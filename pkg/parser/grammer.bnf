template          → ( valueTemplate | TEXT )* ;
valueTemplate     → TEMPLATE_START expression TEMPLATE_END ;
expression        → ternary ;
ternary           → logicOr ( QMARK expression COLON expression )? ;
logicOr           → logicAnd ( OR logicAnd )* ;
logicAnd          → equality ( AND equality )* ;
equality          → comparison ( ( BANG_EQUAL | EQUAL_EQUAL ) comparison )* ;
comparison        → term ( ( GREATER | GREATER_EQUAL | LESS | LESS_EQUAL ) term )* ;
term              → factor ( ( MINUS | PLUS ) factor )* ;
factor            → unary ( ( SLASH | STAR ) unary )* ;
unary             → ( BANG | MINUS ) unary | call ;
call              → primary ( LPAREN arguments? RPAREN | get )* ;
get               → ((QMARK)? DOT identifier | LBRACKET expression RBRACKET) ;
primary           → number | string | TRUE | FALSE | NIL | identifier | LPAREN expression RPAREN | array | map ;
map               → LBRACE ( mapEntry ( COMMA mapEntry )* )? RBRACE ;
mapEntry          → ( identifier | string | LBRACKET expression RBRACKET ) COLON expression ;
array             → LBRACKET ( expression ( COMMA expression )* )? RBRACKET ;
arguments         → expression ( COMMA expression )* ;
identifier        → LETTER ( LETTER | DIGIT )* ;
number            → DIGIT+ ( DOT DIGIT+ )? ;
string            → (DQUOTE characters? DQUOTE) ;
characters        → ( escape | char )* ;
escape            → "\\" char ;
LETTER            → [a-zA-Z] ;
DIGIT             → [0-9] ;
TEMPLATE_START    → "@{{" ;
TEMPLATE_END      → "}}" ;
LBRACE            → "{" ;
RBRACE            → "}" ;
QMARK             → "?" ;
COLON             → ":" ;
COMMA             → "," ;
DOT               → "." ;
EQUAL             → "=" ;
BANG              → "!" ;
MINUS             → "-" ;
PLUS              → "+" ;
DQUOTE            → "\"" ;
SQUOTE            → "\'" ;
SLASH             → "/" ;
STAR              → "*" ;
LPAREN            → "(" ;
RPAREN            → ")" ;
EQUAL_EQUAL       → "==" ;
BANG_EQUAL        → "!=" ;
LBRACKET          → "[" ;
RBRACKET          → "]" ;
GREATER           → ">" ;
GREATER_EQUAL     → ">=" ;
LESS              → "<" ;
LESS_EQUAL        → "<=" ;
AND               → "&&" ;
OR                → "||" ;
TRUE              → "true" ;
FALSE             → "false" ;
NIL               → "nil" ;
TEXT              → [^\{\}]+ ;
char              → [^\"] ;
